#################################################################
#								#
# Copyright 2001, 2014 Fidelity Information Services, Inc	#
#								#
# Copyright (c) 2017-2018 YottaDB LLC. and/or its subsidiaries.	#
# All rights reserved.						#
#								#
# Copyright (c) 2017 Stephen L Johnson. All rights reserved.	#
#								#
#	This source code contains the intellectual property	#
#	of its copyright holder(s), and is made available	#
#	under a license.  If you do not know the terms of	#
#	the license, please stop and do not read further.	#
#								#
#################################################################

	.sbttl	error.si

#	PAGE	+
#-----------------------------------------------
# Mumps error condition handler macros
#-----------------------------------------------
chnd_size	 =	416	/* sizeof(condition_handler) */
chnd_save_active =	0
chnd_ch_active	 =	4
chnd_ch		 =	12
chnd_jmp	 =	16

	.include "gtm_threadgbl_deftypes_asm.si"

	.data
	.extern	ctxt
	.extern	active_ch
	.extern gtm_threadgbl

	.text
#	setjmp is really __sigsetjmp(env,0)
	.extern	__sigsetjmp
	.extern gtm_asm_establish

	.sbttl	error.si    ESTABLISH

.macro ESTABLISH label
        bl	gtm_asm_establish		/* Bulk of ESTABLISH macro */
	ldr	r0, =ctxt
	ldr	r0, [r0]
	ldr	r2, =mdb_condition_handler
	str	r2, [r0, #chnd_ch]		/* ctxt->ch = x */
	mov	r1, #0				/* setjmp(ctxt->jmp) */
	add	r0, #chnd_jmp
	bl 	__sigsetjmp
	cmp	r0, #0
	beq	2f
	/* This return point is when an error was unwound or a MUM_TSTART was run. We need to reduce the
	 * rts_error_depth before doing anything else.
	 */
	ldr	r1, =gtm_threadgbl
	ldr	r1, [r1]
	ldr	r2, [r1, #ggo_rts_error_depth]
	cmp	r2, #0				/* Verify value is non-zero before decrementing */
	beq	2f
	sub	r2, #1
	str	r2, [r1, #ggo_rts_error_depth]
2:
	adds	r0, #1
	bne	1f
	REVERT
	b	return
1:
.endm

	.sbttl	error.si    REVERT
.macro REVERT
	ldr	r3, =ctxt			/* active_ch = ctxt->save_active_c */
	ldr	r0, [r3]
	ldr	r2, [r0, #chnd_save_active]
	ldr	r1, =active_ch
	ldr	r1, [r1]
	str	r2, [r1]
	sub	r0, r0, #chnd_size		/* ctxt-- */
	str	r0, [r3]
.endm
